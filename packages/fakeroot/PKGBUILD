# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=fakeroot
pkgver=1.21
pkgrel=2
pkgdesc='A tool for simulating superuser privileges.'
arch=(x86_64)
url='http://fakeroot.alioth.debian.org/'
license=(GPL)
groups=(base)
depends=(musl)
makedepends=(
    libcap-dev
    libtool
)
options=()
changelog=ChangeLog
source=(
    "http://ftp.de.debian.org/debian/pool/main/f/fakeroot/fakeroot_${pkgver}.orig.tar.gz"
    fakeroot-no64.patch
    fakeroot-stdint.patch
    fakeroot-uclibc.patch
)

sha256sums=(
    5d293c1b3604b2a9265d1811a9890c749887ef08aec55f3237d32bf35836171a
    3420da0789caef582fcfc890c657da4136b3b06d1205443f4409cdf85ab02a46
    8739c0b79c3f2b0e46cd0dffa8b73d946efbe994f61f69f7d1115c2dcec22df1
    e325ad9aad310caa71002adea220795f706c621c5794093f16c0f319b3d58977
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i 's@getopt --version@& 2>/dev/null@' scripts/fakeroot.in
    patch -p1 -i "$srcdir"/fakeroot-no64.patch
    patch -p1 -i "$srcdir"/fakeroot-stdint.patch
    patch -p1 -i "$srcdir"/fakeroot-uclibc.patch
    for d in de es fr nl pt sv ; do
        sed -i '/man_MANS = /s@=.*@=@' "doc/${d}/Makefile.am"
    done
    sed -i 's@puts.*);\]@\]@g' configure.ac
    ./bootstrap
    CFLAGS+=" -D_STAT_VER=0" \
      ./configure \
      --prefix='' \
      --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
