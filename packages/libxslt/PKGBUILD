# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(libxslt libxslt-dev)
pkgver=1.1.29
pkgrel=1
pkgdesc='XSLT libs'
arch=(x86_64)
url='http://xmlsoft.org/'
license=(GPL)
groups=(base)
depends=()
makedepends=(
    libxml2-dev
    zlib-dev
)
options=()
changelog=ChangeLog
source=(
    "ftp://xmlsoft.org/libxslt/libxslt-${pkgver}.tar.gz"
)
sha256sums=(
    b5976e3857837e7617b29f2249ebb5eeac34e249208d31f1fbf7a6ba7a4090ce
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    #-D__GNUC__=2' \
    CFLAGS+=' -fPIC' \
      ./configure --prefix='' \
      --enable-static \
      --enable-shared
    make V=1
}

package_libxslt() {
    pkgfiles=(
        bin/xsltproc
        lib/*.so.*
        share/man/man1
    )
    depends=(libxml2 musl)

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libxslt-dev() {
    pkgfiles=(
        bin/xslt-config
        include
        lib/*.a
        lib/*.so
        lib/xsl*.sh
        lib/pkgconfig
        share/man/man3
    )
    depends=(libxslt libxml2-dev)

    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
