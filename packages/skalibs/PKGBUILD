# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(
    skalibs
    skalibs-leapsecs
    skalibs-dev
)
pkgver=2.5.0.0
pkgrel=1
pkgdesc='A library suite supporting skarnet.org software.'
arch=(x86_64)
url=http://skarnet.org/software/skalibs/
license=(ISC)
groups=(base)
depends=()
makedepends=()
options=()
changelog=ChangeLog

source=(
    "http://skarnet.org/software/skalibs/${pkgname}-${pkgver}.tar.gz"
)

sha256sums=(
    38408ff6d0aec581010ecf9e49703ec5f4c8887bbe68717ec087634a7ade849c
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure \
      --prefix=/ \
      --disable-ipv6 \
      --enable-force-devr \
      --enable-tai-clock
    make
}

package_skalibs() {
    pkgfiles=(
        lib/libskarnet.so.*
    )
    depends=(
        musl
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_skalibs-leapsecs() {
    pkgfiles=(
        etc/leapsecs.dat
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_skalibs-dev() {
    pkgfiles=(
        include/skalibs
        lib/skalibs/sysdeps
        lib/libskarnet.a
        lib/libskarnet.so
    )
    depends=(
        skalibs
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    mv lib/skalibs/libskarnet.a lib
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
