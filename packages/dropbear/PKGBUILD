# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=dropbear
pkgver=2018.76
pkgrel=2
pkgdesc='A relatively small SSH server and client.'
arch=(x86_64)
url='http://matt.ucc.asn.au/dropbear/dropbear.html'
license=(MIT)
groups=(base)
depends=(s6 execline)
makedepends=(zlib-dev)
options=()
changelog=ChangeLog
source=(
    "http://matt.ucc.asn.au/dropbear/releases/dropbear-${pkgver}.tar.bz2"
    dropbear-service
    dropbear-log
    dropbear-finish
    dropbear.install
)

sha256sums=(
    f2fb9167eca8cf93456a5fc1d4faf709902a3ab70dd44e352f3acbc3ffdaea65
    743b0d8c6e7a60f07910daa913bfb5717e98b47654ac7cffaafff15a1c3a7566
    992a38e175c6dd8dea0c0496dbe175749f0f18eee6d861d913cd8f5bfe19a37a
    678981da5f219b6d335e132073ce5516e51470f80d17f33b282af259d846c5b0
    8e70536d7bdf7d8cd7ae0b7d0b047e59c424374495490fe02140989345baa886
)

install=dropbear.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    export CFLAGS="$CFLAGS -DDEFAULT_RECV_WINDOW=1048576 -DRECV_MAX_PAYLOAD_LEN=524288"
    ./configure \
      --prefix='' \
      --enable-static \
      --disable-wtmp
    make PROGRAMS="dropbear dbclient dropbearkey dropbearconvert scp"
}

package() {
    options+=(emptydirs)
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="$pkgdir" \
      PROGRAMS="dropbear dbclient dropbearkey dropbearconvert scp" \
      install
    ln -s dbclient "${pkgdir}/bin/ssh"
    install -d -m 0755 "${pkgdir}/etc/dropbear"
    install -d "${pkgdir}/etc/s6/services/available/dropbear/log"
    install -m 0754 "${srcdir}/dropbear-service" \
        "${pkgdir}/etc/s6/services/available/dropbear/run"
    install -m 0754 "${srcdir}/dropbear-finish" \
        "${pkgdir}/etc/s6/services/available/dropbear/finish"
    install -m 0754 "${srcdir}/dropbear-log" \
        "${pkgdir}/etc/s6/services/available/dropbear/log/run"
    rm -rf "$pkgdir"/share
}
