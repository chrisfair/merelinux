# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

rationale='This is part of the core toolchain'
pkgname=(musl musl-dev)
pkgver=1.1.20
pkgrel=1
pkgdesc='An implementation of the C/POSIX standard library.'
arch=('x86_64')
url='http://www.musl-libc.org'
license=(LGPL BSD)
groups=('base')
depends=()
makedepends=()
options=()
changelog=ChangeLog
source=(
    "http://www.etalabs.net/musl/releases/${pkgname}-${pkgver}.tar.gz"
)
sha256sums=(
    44be8771d0e6c6b5f82dd15662eb2957c9a3173a19a8b49966ac0542bbd40d61
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    unset CFLAGS CXXFLAGS
    ./configure --prefix=/
    make
}

package_musl() {
    pkgfiles=(
        bin/ldd
        lib/libc.so
        "lib/ld-musl-${arch}.so.1"
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    install -d bin
    ln -sf ../lib/libc.so bin/ldd
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}

package_musl-dev() {
    pkgfiles=(
        include
        lib/*.a
        lib/*.o
    )
    depends=(musl linux-headers)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}
