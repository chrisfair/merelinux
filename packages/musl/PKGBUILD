pkgname=('musl' 'musl-devel')
pkgver=0.9.12
pkgrel=1
pkgdesc='An implementation of the C/POSIX standard library.'
arch=('x86_64')
url='http://www.etalabs.net/musl'
license=('LGPL' 'BSD')
groups=('base')
depends=()
makedepends=()
options=('strip')
changelog=ChangeLog
source=(
	"http://www.etalabs.net/musl/releases/$pkgname-$pkgver.tar.gz"
)
sha256sums=(
	'72353f9e1f2fdaf5e88223dcd1cfa5555f5a364eb5e4a9967de1fd44cc4c6d3e'
)


build() {
	cd "$srcdir/$pkgbase-$pkgver"
	CFLAGS='' ./configure --prefix=''
	make $MAKEFLAGS
}

package_musl() {
	pkgfiles=(
		'bin/ldd'
		'lib/libc.so'
	)
	cd "$srcdir/$pkgbase-$pkgver"
	make DESTDIR="$pkgdir" install
	ln -sf libc.so $pkgdir/lib/ld-musl-$arch.so.1
	install -d "$pkgdir/bin"
	ln -sf ../lib/libc.so "$pkgdir/bin/ldd"
}

package_musl-devel() {
	depends=('musl')
	cd "$srcdir/$pkgbase-$pkgver"
	make DESTDIR="$pkgdir" install
	rm "$pkgdir/lib/libc.so"
}
