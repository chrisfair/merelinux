pkgname=(musl musl-dev)
pkgver=1.1.10
pkgrel=1
pkgdesc='An implementation of the C/POSIX standard library.'
arch=('x86_64')
url='http://www.musl-libc.org'
license=(LGPL BSD)
groups=('base')
depends=()
makedepends=()
options=(!upx)
changelog=ChangeLog
source=(
    "http://www.etalabs.net/musl/releases/${pkgname}-${pkgver}.tar.gz"
)
sha256sums=(
    45bbe9b1c7f7a0f743477af1e103b6889bfe4dd9815e16f6c89f6c90831c8b7c
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    CFLAGS='' ./configure --prefix=/
    make $MAKEFLAGS
}

package_musl() {
    pkgfiles=(
        bin/ldd
        lib/libc.so
        "lib/ld-musl-${arch}.so.1"
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    install -d bin
    ln -sf ../lib/libc.so bin/ldd
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_musl-dev() {
    depends=('musl')
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="$pkgdir" install
    rm "${pkgdir}/lib/libc.so"
    rm "${pkgdir}/lib/ld-musl-${arch}.so.1"
}
