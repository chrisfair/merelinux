pkgname=(musl musl-dev)
pkgver=1.1.15
pkgrel=1
pkgdesc='An implementation of the C/POSIX standard library.'
arch=('x86_64')
url='http://www.musl-libc.org'
license=(LGPL BSD)
groups=('base')
depends=()
makedepends=(mksh)
options=(!upx)
changelog=ChangeLog
source=(
    "http://www.etalabs.net/musl/releases/${pkgname}-${pkgver}.tar.gz"
)
sha256sums=(
    97e447c7ee2a7f613186ec54a93054fe15469fe34d7d323080f7ef38f5ecb0fa
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    CFLAGS='' mksh ./configure --prefix=/
    make $MAKEFLAGS
}

package_musl() {
    pkgfiles=(
        bin/ldd
        lib/libc.so
        "lib/ld-musl-${arch}.so.1"
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    install -d bin
    ln -sf ../lib/libc.so bin/ldd
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_musl-dev() {
    depends=('musl')
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="$pkgdir" install
    rm "${pkgdir}/lib/libc.so"
    rm "${pkgdir}/lib/ld-musl-${arch}.so.1"
}
