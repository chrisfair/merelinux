# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(bison bison-dev)
pkgver=3.3.2
pkgrel=1
pkgdesc='A general-purpose parser generator.'
arch=('x86_64')
url='http://www.gnu.org/software/bison/'
license=('GPL3')
groups=('base')
depends=(m4 musl)
makedepends=(perl)
options=()
changelog=ChangeLog
source=(
    "http://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"
)

sha256sums=(
    039ee45b61d95e5003e7e8376f9080001b4066ff357bde271b7faace53b9d804
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=''
    make
}

package_bison() {
    pkgfiles=(
        bin/bison
        bin/yacc
        share/bison
        share/man/man1/bison.1
        share/man/man1/yacc.1
        share/aclocal/bison-i18n.m4
    )
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_bison-dev() {
    pkgfiles=(
        lib/liby.a
    )
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
