# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(bison bison-dev)
pkgver=3.2
pkgrel=1
pkgdesc='A general-purpose parser generator.'
arch=('x86_64')
url='http://www.gnu.org/software/bison/'
license=('GPL3')
groups=('base')
depends=(m4 musl)
makedepends=(perl)
options=()
changelog=ChangeLog
source=(
    "http://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"
)

sha256sums=(
    deec377b95aa72ec4e1a33fe2c938d2480749d740b5291a7cc1d77808d3710bf
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=''
    make
}

package_bison() {
    pkgfiles=(
        bin/bison
        bin/yacc
        share/bison
        share/man/man1/bison.1
        share/man/man1/yacc.1
        share/aclocal/bison-i18n.m4
    )
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_bison-dev() {
    pkgfiles=(
        lib/liby.a
    )
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
