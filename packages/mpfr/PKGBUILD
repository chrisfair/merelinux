# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(mpfr mpfr-dev)
pkgver=4.0.1
pkgrel=1
pkgdesc='A library for multiple-precision floating-point computations with correct rounding.'
arch=('x86_64')
url='http://www.mpfr.org'
license=(GPL)
groups=(base)
depends=()
makedepends=(gmp-dev)
options=()
changelog=ChangeLog
source=(
    "http://www.mpfr.org/mpfr-current/${pkgname}-${pkgver}.tar.xz"
)
sha256sums=(
    67874a60826303ee2fb6affc6dc0ddd3e749e9bfcb4c8655e3953d0458a6e16e
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure --prefix='' \
      --enable-thread-safe
    make
}

check() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make check
}

package_mpfr() {
    pkgfiles=(
        lib/libmpfr.so.*
    )
    depends=(musl gmp)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}

package_mpfr-dev() {
    pkgfiles=(
        include
        lib/libmpfr.a
        lib/libmpfr.so
    )
    depends=(mpfr)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}
