# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(mpfr mpfr-dev)
pkgver=4.0.2
pkgrel=1
pkgdesc='A library for multiple-precision floating-point computations with correct rounding.'
arch=('x86_64')
url='http://www.mpfr.org'
license=(GPL)
groups=(base)
depends=()
makedepends=(gmp-dev)
options=()
changelog=ChangeLog
source=(
    "http://www.mpfr.org/mpfr-${pkgver}/${pkgname}-${pkgver}.tar.xz"
)
sha256sums=(
    1d3be708604eae0e42d578ba93b390c2a145f17743a744d8f3f8c2ad5855a38a
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure --prefix='' \
      --enable-thread-safe
    make
}

check() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make check
}

package_mpfr() {
    pkgfiles=(
        lib/libmpfr.so.*
    )
    depends=(musl gmp)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}

package_mpfr-dev() {
    pkgfiles=(
        include
        lib/libmpfr.a
        lib/libmpfr.so
    )
    depends=(mpfr)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}
