pkgname=(mpfr mpfr-dev)
pkgver=3.1.3
pkgrel=1
pkgdesc='A library for multiple-precision floating-point computations with correct rounding.'
arch=('x86_64')
url='http://www.mpfr.org'
license=(GPL)
groups=(base)
depends=()
makedepends=(gmp-dev)
options=()
changelog=ChangeLog
source=(
    "http://www.mpfr.org/mpfr-current/${pkgname}-${pkgver}.tar.xz"
)
sha256sums=(
    6835a08bd992c8257641791e9a6a2b35b02336c8de26d0a8577953747e514a16
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure --prefix='' \
      --enable-thread-safe
    make $MAKEFLAGS
    make check
}

package_mpfr() {
    pkgfiles=(
        lib/libmpfr.so.*
    )
    depends=(musl gmp)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -pudmv "${pkgdir}"
}

package_mpfr-dev() {
    pkgfiles=(
        include
        lib/libmpfr.a
        lib/libmpfr.so
    )
    depends=(mpfr)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -pudmv "${pkgdir}"
}
