# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(mpfr mpfr-dev)
pkgver=3.1.5
pkgrel=1
pkgdesc='A library for multiple-precision floating-point computations with correct rounding.'
arch=('x86_64')
url='http://www.mpfr.org'
license=(GPL)
groups=(base)
depends=()
makedepends=(gmp-dev)
options=()
changelog=ChangeLog
source=(
    "http://www.mpfr.org/mpfr-current/${pkgname}-${pkgver}.tar.xz"
)
sha256sums=(
    015fde82b3979fbe5f83501986d328331ba8ddf008c1ff3da3c238f49ca062bc
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure --prefix='' \
      --enable-thread-safe
    make $MAKEFLAGS
    make check
}

package_mpfr() {
    pkgfiles=(
        lib/libmpfr.so.*
    )
    depends=(musl gmp)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -pudmv "${pkgdir}"
}

package_mpfr-dev() {
    pkgfiles=(
        include
        lib/libmpfr.a
        lib/libmpfr.so
    )
    depends=(mpfr)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -pudmv "${pkgdir}"
}
