# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(npth npth-dev)
pkgver=1.3
pkgrel=1
pkgdesc='The New Portable Threads Library'
arch=(x86_64)
url='https://www.gnupg.org/'
license=(GPL3)
groups=(base)
depends=()
makedepends=(libtool)
options=()
changelog=ChangeLog
source=(
    "https://www.gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
)

sha256sums=(
    bca81940436aed0734eb8d0ff8b179e04cc8c087f5625204419f5f45d736a82a
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./autogen.sh
    CFLAGS+=' -fPIC' ./configure --prefix='' \
        --enable-static
    make
}

package_npth() {
    pkgfiles=(
        lib/libnpth.so.*
    )
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_npth-dev() {
    pkgfiles=(
        bin
        include
        lib/libnpth.a
        lib/libnpth.so
        share/aclocal
    )
    depends=(npth)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
