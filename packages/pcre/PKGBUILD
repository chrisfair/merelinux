# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(
    pcre
    libpcre
    libpcre-dev
)
pkgver=8.37
pkgrel=1
pkgdesc='Perl Compatible Regular Expressions'
arch=(x86_64)
url='http://pcre.org'
license=(GPL)
groups=(base)
depends=()
makedepends=(gcc-c++)
options=()
changelog=ChangeLog
source=(
    "ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/pcre-${pkgver}.tar.bz2"
)
sha256sums=(
    51679ea8006ce31379fb0860e46dd86665d864b5020fc9cd19e71260eef4789d
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure \
      --prefix=''
    make $MAKEFLAGS
}

package_pcre() {
    pkgfiles=(
        bin
    )
    depends=(
        libpcre
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    rm bin/pcre-config
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_libpcre() {
    pkgfiles=(
        "lib/libpcre*.so.*"
    )
    depends=(
        musl
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_libpcre-dev() {
    pkgfiles=(
        bin/pcre-config
        include
        "lib/*.so"
        "lib/*.a"
    )
    depends=(
        libpcre
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
