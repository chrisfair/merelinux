# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(pcre libpcre libpcre-dev)
pkgver=8.42
pkgrel=1
pkgdesc='Perl Compatible Regular Expressions'
arch=(x86_64)
url='http://pcre.org'
license=(GPL)
groups=(base)
depends=()
makedepends=(libtool)
options=()
changelog=ChangeLog
source=(
    "ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/pcre-${pkgver}.tar.bz2"
)
sha256sums=(
    2cd04b7c887808be030254e8d77de11d3fe9d4505c39d4b15d2664ffe8bf9301
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    autoreconf -i
    ./configure --prefix='' \
        --enable-utf8 \
        --enable-unicode-properties
    make
}

package_pcre() {
    pkgfiles=(
        bin
    )
    depends=(
        libpcre
        musl
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
    rm "${pkgdir}/bin/pcre-config"
}

package_libpcre() {
    pkgfiles=(
        lib/libpcre*.so.*
    )
    depends=(musl)

    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libpcre-dev() {
    pkgfiles=(
        bin/pcre-config
        include
        lib/*.so
        lib/*.a
        lib/pkgconfig
    )
    depends=(libpcre)

    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
