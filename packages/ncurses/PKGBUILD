pkgname=('ncurses' 'ncurses-devel')
pkgver=5.9
pkgrel=1
pkgdesc='An API for writing text-based user interfaces.'
arch=('x86_64')
url='http://www.gnu.org/software/ncurses'
license=('GPL2')
groups=('base')
depends=()
makedepends=(musl)
options=() # Defaults in makepkg.conf
changelog=ChangeLog

source=(
	"http://ftp.gnu.org/gnu/ncurses/$pkgname-$pkgver.tar.gz"
	'ncurses-fallback.c'
)
sha256sums=(
	'9046298fb440324c9d4135ecea7879ffed8546dd1b58e59430ea07a4633f563b'
	'b7981bb4e087ee4d3cf796541d4467a6db2d5fa494ff3cb12924cbdbd962135c'
)


build() {
	cd "$srcdir/$pkgname-$pkgver"
	cp "$srcdir/ncurses-fallback.c" ncurses/fallback.c
	./configure \
	  --prefix='' \
	  --with-fallbacks="linux vt100 xterm xterm256-color" \
	  --with-shared \
	  --disable-debug \
  	  --disable-nls \
  	  --includedir=/include \
	  --mandir=/share/man
	make $MAKEFLAGS
}

package_ncurses() {
	pkgfiles=(
		'bin'
		'lib'
		'share/terminfo'
		'share/tabset'
		'share/man/man1'
		'share/man/man5'
		'share/man/man7'
	)
	cd "$srcdir/$pkgbase-$pkgver"
	make DESTDIR=${pkgdir} install
	find "$pkgdir/lib" -name "*.a" -delete
	find "$pkgdir/lib" -name "*.so" -delete
	rm -f "$pkgdir"/bin/{clear,reset,ncurses5-config}
}

package_ncurses-devel() {
	pkgfiles=(
		'bin/ncurses5-config'
		'include'
		'lib/libcurses.so'
		'lib/libncurses.so'
		'lib/libmenu.so'
		'lib/libpanel.so'
		'lib/libcurses.a'
		'lib/libncurses.a'
		'lib/libmenu_g.a'
		'lib/libncurses_g.a'
		'lib/libpanel_g.a'
		'lib/libncurses++.a'
		'lib/libform_g.a'
		'lib/libpanel.a'
		'lib/libform.a'
		'lib/libmenu.a'
		'share/man/man3'
	)
	depends=(
		'ncurses'
	)
	cd "$srcdir/$pkgbase-$pkgver"
	make DESTDIR=${pkgdir} install
	ln -s libncurses.so ${pkgdir}/lib/libcurses.so
	ln -s libncurses.a ${pkgdir}/lib/libcurses.a
}
