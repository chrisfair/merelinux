# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(
    ncurses
    ncurses-terminfo
    libncurses-dev
)
pkgver=6.0
pkgrel=1
pkgdesc='An API for writing text-based user interfaces.'
arch=(x86_64)
url='http://www.gnu.org/software/ncurses'
license=(GPL2)
groups=(base)
depends=()
makedepends=(
    ncurses
    ncurses-terminfo
)
options=()
changelog=ChangeLog

source=(
    "http://ftp.gnu.org/gnu/ncurses/${pkgname}-${pkgver}.tar.gz"
    ftp://invisible-island.net/ncurses/termcap.src.gz
)
sha256sums=(
    f551c24b30ce8bfb6e96d9f59b42fbea30fa3a6123384172f9e7284bcf647260
    2ac1f97574e1e07a7fd37e6ba324a5c474bdb9ef537b061127d9f18d467b501c
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    zcat "$srcdir"/termcap.src.gz >termcap
    ./ncurses/tinfo/MKfallback.sh /share/terminfo/ termcap \
        tic linux vt100 xterm xterm-256color >ncurses/fallback.c
    CFLAGS="$CFLAGS -fPIC" \
        LDFLAGS='-Wl,-static' \
        ./configure \
        --prefix='' \
        --with-fallbacks="linux vt100 xterm xterm256-color" \
        --disable-debug \
        --without-tests \
        --disable-nls \
        --without-cxx-binding \
        --without-dlsym \
        --enable-widec \
        --enable-sigwinch \
        --includedir=/include \
        --mandir=/share/man
    make
}

package_ncurses() {
    pkgfiles=(bin)

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install

    cd "${pkgdirbase}/dest"
    for lib in ncurses form panel menu ; do
        ln -s lib${lib}w.a lib/lib${lib}.a
    done
    ln -s libncurses.a lib/libcurses.a
    ln -s ncursesw include/ncurses

    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
    # ncurses6w-config is in -dev and clear/reset are in busybox
    rm "$pkgdir"/bin/{ncursesw6-config,clear,reset}
}

package_ncurses-terminfo() {
    pkgfiles=(share/terminfo)
    depends=(ncurses)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libncurses-dev() {
    pkgfiles=(
        bin/ncursesw6-config
        include
        lib/*.a
    )
    cd "${pkgdirbase}/dest"
    ln -s ncursesw6-config bin/ncurses6-config
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
