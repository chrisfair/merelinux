pkgname=(
    ncurses
    libncurses
    libncurses-devel
)
pkgver=5.9
pkgrel=3
pkgdesc='An API for writing text-based user interfaces.'
arch=(x86_64)
url='http://www.gnu.org/software/ncurses'
license=(GPL2)
groups=(base)
depends=()
makedepends=(gcc-c++)
options=()
changelog=ChangeLog

source=(
    "http://ftp.gnu.org/gnu/ncurses/${pkgname}-${pkgver}.tar.gz"
    ncurses-fallback.c
)
sha256sums=(
    9046298fb440324c9d4135ecea7879ffed8546dd1b58e59430ea07a4633f563b
    b7981bb4e087ee4d3cf796541d4467a6db2d5fa494ff3cb12924cbdbd962135c
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    cp "${srcdir}/ncurses-fallback.c" ncurses/fallback.c
    CFLAGS="$CFLAGS -fPIC" \
    ./configure \
      --prefix='' \
      --with-fallbacks="linux vt100 xterm xterm256-color" \
      --with-shared \
      --disable-debug \
      --disable-nls \
      --enable-widec \
      --includedir=/include \
      --mandir=/share/man
    make $MAKEFLAGS
}

package_ncurses() {
    pkgfiles=(
        bin
    )
    depends=(
        libncurses
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    rm bin/ncursesw5-config
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_libncurses() {
    pkgfiles=(
        lib
    )
    depends=(
        musl
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find . -name "*.so" -delete
    find . -name "*.a" -delete
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_libncurses-devel() {
    pkgfiles=(
        bin/ncursesw5-config
        include
        "lib/*.so"
        "lib/*.a"
    )
    depends=(
        libncurses
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    for lib in ncurses form panel menu ; do
        ln -s lib${lib}w.so lib/lib${lib}.so
        ln -s lib${lib}w.a lib/lib${lib}.a
    done
    ln -s libncurses.so lib/libcurses.so
    ln -s libncurses.a lib/libcurses.a
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
