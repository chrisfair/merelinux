# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(python python-dev)
_vermajor=3
_verminor=7
pkgver=${_vermajor}.${_verminor}.1
pkgrel=1
pkgdesc='A clear and powerful object-oriented programming language,'
arch=(x86_64)
url='http://www.python.org'
license=(Python)
groups=(base)
depends=()
makedepends=(
    libffi-dev
    libncurses-dev
    libressl-dev
    libsqlite-dev
    liblzma-dev
    readline-dev
    zlib-dev
)
options=()
changelog=ChangeLog

source=(
    "https://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz"
    python-fix-ctypes_util.patch
    fix-root-prefix.patch
)
sha256sums=(
    fa7e2b8e8c9402f192ad56dc4f814089d1c4466c97d780f5e5acc02c04243d6d
    db33506472cb9819acf03e8206323ed0aea3cb0ed537b047976dc613e9a34e4e
    b363ebe8bd810545ca00b52398fa86c60e232c61191af560b02a85b6e1308e42
)


build() {
    cd "${srcdir}/Python-${pkgver}"
    patch -Np1 -i "${srcdir}/python-fix-ctypes_util.patch"
    patch -Np0 -i "${srcdir}/fix-root-prefix.patch"
    unset CFLAGS CXXFLAGS
    CC='gcc -fPIC' CXX='g++ -fPIC' ./configure \
        --prefix=/ \
        --with-system-ffi
    make
}

check() {
    cd "${srcdir}/Python-${pkgver}"
    #make test
}

package_python() {
    depends=(
        musl
        libffi
        libressl
        libsqlite
        readline
    )
    pkgfiles=(
        bin/pip
        "bin/pip${_vermajor}"
        bin/python
        "bin/python${_vermajor}"
        "bin/python${_vermajor}.${_verminor}"
        "lib/python${_vermajor}.${_verminor}"
        "include/python${_vermajor}.${_verminor}m/pyconfig.h"
    )

    cd "${srcdir}/Python-${pkgver}"
    make DESTDIR="${pkgdirbase}/destdir" install
    cd "${pkgdirbase}/destdir"
    ln -s "python${_vermajor}.${_verminor}" bin/python
    ln -s "pip${_vermajor}" bin/pip
    find . -name "*.pyc" -delete -o -name "*.pyo" -delete
    find . -name 'test' -type d -exec rm -rf '{}' + || true
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}

package_python-dev() {
    depends=(
        python
    )
    pkgfiles=(
        "bin/py*-config"
        include
        "lib/libpython*.a"
        lib/pkgconfig
    )

    cd "${pkgdirbase}/destdir"
    rm "include/python${_vermajor}.${_verminor}m/pyconfig.h"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}
