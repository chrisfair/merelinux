pkgname=(python python-dev)
vermajor=3
verminor=5
pkgver=${vermajor}.${verminor}.2
pkgrel=1
pkgdesc='The Python programming language'
arch=(x86_64)
url='http://www.python.org'
license=(Python)
groups=(base)
depends=()
makedepends=(
    gcc-c++
    libffi-dev
    libressl-dev
    libncurses-dev
    readline-dev
    libz-dev
)
options=()
changelog=ChangeLog

source=(
    "https://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.xz"
    python-fix-ctypes_util.patch
    fix-root-prefix.patch
)
sha256sums=(
    0010f56100b9b74259ebcd5d4b295a32324b58b517403a10d1a2aa7cb22bca40
    b9b817a4fd8dea3ef28929a9bee6a3572e0a365d815615aa112192f78ae6d401
    b363ebe8bd810545ca00b52398fa86c60e232c61191af560b02a85b6e1308e42
)


build() {
    cd "${srcdir}/Python-${pkgver}"
    patch -Np1 -i "${srcdir}/python-fix-ctypes_util.patch"
    patch -Np0 -i "${srcdir}/fix-root-prefix.patch"
    CC='gcc -fPIC' \
      ./configure \
        --prefix=/ \
        --with-system-ffi
    make $MAKEFLAGS
}

check() {
    cd "${srcdir}/Python-${pkgver}"
    #make test
}

package_python() {
    depends=(
        musl
        libffi
        libz
    )
    pkgfiles=(
        bin/pip
        "bin/pip${vermajor}"
        bin/python
        "bin/python${vermajor}"
        "bin/python${vermajor}.${verminor}"
        "lib/python${vermajor}.${verminor}"
        "include/python${vermajor}.${verminor}m/pyconfig.h"
    )

    cd "${srcdir}/Python-${pkgver}"
    make DESTDIR="${pkgdirbase}/destdir" install
    cd "${pkgdirbase}/destdir"
    ln -s "python${vermajor}.${verminor}" bin/python
    ln -s "pip${vermajor}" bin/pip
    find . -name "*.pyc" -delete -o -name "*.pyo" -delete
    find . -name 'test' -type d -exec rm -rf '{}' + || true
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_python-dev() {
    depends=(
        python
    )
    pkgfiles=(
        "bin/py*-config"
        include
        "lib/libpython*.a"
        lib/pkgconfig
    )

    cd "${pkgdirbase}/destdir"
    rm "include/python${vermajor}.${verminor}m/pyconfig.h"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
