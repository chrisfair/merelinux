# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

rationale='Many packages use the pkg-config functionality to locate libraries'
pkgname=pkgconf
pkgver=1.5.4
pkgrel=1
pkgdesc='An API-driven pkg-config replacement'
arch=('x86_64')
url='https://github.com/pkgconf/pkgconf'
license=('BSD')
groups=('base')
depends=()
makedepends=(libtool)
options=()
changelog=ChangeLog
source=(
    "${url}/archive/pkgconf-${pkgver}.tar.gz"
)

sha256sums=(
    182bed8249100e42f1f77b6d2bbc73d669281e8739dd9620f5d3b4f0a136d2ca
)


build() {
    cd "${srcdir}/pkgconf-pkgconf-${pkgver}"
    ./autogen.sh
    LDFLAGS='-Wl,-static' \
      ./configure \
      --prefix='' \
      --disable-shared \
      --with-system-libdir=/lib \
      --with-system-includedir=/include
    make
}

package() {
    cd "${srcdir}/pkgconf-pkgconf-${pkgver}"
    make DESTDIR="$pkgdir" install
    ln -s pkgconf "${pkgdir}/bin/pkg-config"
}
