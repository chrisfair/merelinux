# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

rationale='Many packages use the pkg-config functionality to locate libraries'
pkgname=pkgconf
pkgver=1.6.0
pkgrel=1
pkgdesc='An API-driven pkg-config replacement'
arch=('x86_64')
url='https://github.com/pkgconf/pkgconf'
license=('BSD')
groups=('base')
depends=()
makedepends=(libtool)
options=()
changelog=ChangeLog
source=(
    "${url}/archive/pkgconf-${pkgver}.tar.gz"
)

sha256sums=(
    b4428d70a7f38bd920b6dee617e1c0264afad7a747200715a9a46de9230067c8
)


build() {
    cd "${srcdir}/pkgconf-pkgconf-${pkgver}"
    ./autogen.sh
    LDFLAGS='-Wl,-static' \
      ./configure \
      --prefix='' \
      --disable-shared \
      --with-system-libdir=/lib \
      --with-system-includedir=/include
    make
}

package() {
    cd "${srcdir}/pkgconf-pkgconf-${pkgver}"
    make DESTDIR="$pkgdir" install
    ln -s pkgconf "${pkgdir}/bin/pkg-config"
}
