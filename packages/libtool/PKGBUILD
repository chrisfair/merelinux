# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(libtool libltdl libltdl-dev)
pkgver=2.4.6
pkgrel=2
pkgdesc='A generic library support script.'
arch=(x86_64)
url='http://www.gnu.org/software/libtool'
license=(GPL3)
groups=(base)
depends=()
makedepends=()
options=()
changelog=ChangeLog
source=(
    "http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz"
)

sha256sums=(
    7c87a8c2c8c0fc9cd5019e402bed4292462d00a718a7cd5f11218153bf28b26f
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix='' \
        --enable-static
    make
}

package_libtool() {
    pkgfiles=(
        bin
        share/aclocal
        share/libtool
        share/man/man1
    )
    depends=(
        autoconf
        automake
        bash
    )
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    rm -rf share/info share/doc
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libltdl() {
    pkgfiles=(
        lib/lib*.so.*
    )
    depends=(musl)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libltdl-dev() {
    pkgfiles=(
        include
        lib/lib*.a
        lib/lib*.so
    )
    depends=(libltdl)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
