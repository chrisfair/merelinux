# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(gpgme gpgme-dev)
pkgver=1.6.0
pkgrel=1
pkgdesc='GnuPG Made Easy - a library designed to make access to GnuPG easier for applications'
arch=(x86_64)
url='https://www.gnupg.org/'
license=(GPL3)
groups=(base)
depends=(libgpg-error libassuan)
makedepends=(libgpg-error-dev libassuan-dev)
options=()
changelog=ChangeLog
source=(
    "https://www.gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
)

sha256sums=(
    b09de4197ac280b102080e09eaec6211d081efff1963bf7821cf8f4f9916099d
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
      ./configure --prefix='' \
        --enable-static
    make $MAKEFLAGS
}

package_gpgme() {
    pkgfiles=(
        lib/libgpgme.so.*
        lib/libgpgme-pthread.so.*
    )
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_gpgme-dev() {
    pkgfiles=(
        bin
        include
        lib/libgpgme.a
        lib/libgpgme.so
        lib/libgpgme-pthread.a
        lib/libgpgme-pthread.so
        share/aclocal
        share/common-lisp
    )
    depends=(gpgme libgpg-error-dev libassuan-dev)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
