# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

rationale='Bash is the language used to interpret pacman PKGBUILD files and the makepkg utility that ships with pacman'
pkgname=bash
pkgver=4.4.19
_archivever=4.4.18
pkgrel=1
pkgdesc='A full-featured sh-compatible shell.'
arch=('x86_64')
url='http://www.gnu.org/software/bash'
license=('GPL3')
groups=('base')
depends=()
makedepends=(libncurses-dev readline-dev)
options=()
changelog=ChangeLog

source=(
    "http://ftp.gnu.org/gnu/bash/${pkgname}-${_archivever}.tar.gz"
    'https://ftp.gnu.org/gnu/bash/bash-4.4-patches/bash44-019'
    bashrc
)

sha256sums=(
    604d9eec5e4ed5fd2180ee44dd756ddca92e0b6aa4217bbab2b6227380317f23
    27170d6edfe8819835407fdc08b401d2e161b1400fe9d0c5317a51104c89c11e
    29e174a59b56444b205541cbd3ce9c854d7f47eba0bdb6ea5e823bf3177af557
)


build() {
    cd "${srcdir}/${pkgname}-${_archivever}"
    for patch in ${source[@]} ; do
        case ${patch##*/} in
            bash44-[0-9][0-9][0-9])
                patch -Np0 -i "${srcdir}/${patch##*/}"
                ;;
        esac
    done
    CFLAGS="-O2 -pipe -fno-asynchronous-unwind-tables" \
        ./configure --prefix=/ \
        --enable-static-link \
        --without-bash-malloc \
        --with-installed-readline
    make
}

package() {
    pkgfiles=(
        bin/bash
        etc/bashrc
        share/man/man1/bash.1*
    )
    cd "${srcdir}/${pkgname}-${_archivever}"
    make DESTDIR="${pkgdirbase}/destdir" install
    cd "${pkgdirbase}/destdir"
    install -d etc
    install -m 0644 ${srcdir}/bashrc etc/
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
