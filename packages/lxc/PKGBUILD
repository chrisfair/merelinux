pkgname=(lxc lxc-dev)
pkgver=2.0.6
pkgrel=1
pkgdesc='A userspace interface for the Linux kernel containment features'
arch=(x86_64)
url='https://linuxcontainers.org/'
license=(GPL2)
groups=(base)
depends=()
makedepends=(libcap-dev)
options=()
changelog=ChangeLog
source=(
    "https://linuxcontainers.org/downloads/lxc/lxc-${pkgver}.tar.gz"
    lxc-merebuild
)

sha256sums=(
    7c292cd0055dac1a0e6fbb6a7740fd12b6ffb204603c198faf37c11c9d6dcd7a
    cc0a1d731c9e4172125ca2779ea2b0951df87916616d0352a3318ced2a568dfe
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i '/if_bridge.h/d' src/lxc/lxc_user_nic.c
    ./configure \
      --prefix='' \
      --libexecdir=/lib/lxc
    make $MAKEFLAGS
}

package_lxc() {
    pkgfiles=(
        bin
        etc
        lib/liblxc.so.*
        lib/lxc
        share/lxc
        var
    )
    options+=(emptydirs)
    depends=(acl libcap)
    backup=(etc/lxc/default.conf)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
    set -o pipefail
    install -m 0755 "${srcdir}/lxc-merebuild" "${pkgdir}/share/lxc/templates/"
}

package_lxc-dev() {
    pkgfiles=(
        include
        lib/liblxc.so
        lib/pkgconfig
    )
    depends=(lxc libcap-dev)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
