# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

rationale='The scripted build mechanism for final packages uses LXC'
pkgname=(lxc lxc-dev)
pkgver=2.1.1
pkgrel=3
pkgdesc='A userspace interface for the Linux kernel containment features'
arch=(x86_64)
url='https://linuxcontainers.org/'
license=(GPL2)
groups=(base)
depends=()
makedepends=(libcap-dev)
options=()
changelog=ChangeLog
source=(
    "https://linuxcontainers.org/downloads/lxc/lxc-${pkgver}.tar.gz"
    lxc-merebuild
    autodev.hook.sh
)

sha256sums=(
    68663a67450a8d6734e137eac54cc7077209fb15c456eec401a2c26e6386eff6
    b7c3c02adcfe1f1da29b6f939ed8ef248d0256bf7442dff270ce1cc99bdbcaf6
    2f3361a27efb4822cea733d02761003e8b20411850a61c0403354c698b865d73
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # The install phase seems to expect a shared library will be built
    sed -i '/mv .*liblxc.so/d' src/lxc/Makefile.in
    CFLAGS+=' -static' \
    LDFLAGS='-Wl,-static' \
    ./configure \
      --prefix='' \
      --disable-werror \
      --enable-static \
      --libexecdir=/lib/lxc \
      --with-init-script=none
    make
}

package_lxc() {
    pkgfiles=(
        bin
        etc
        lib/lxc
        share/lxc
        var
    )
    options+=(emptydirs)
    depends=()
    backup=(etc/lxc/default.conf)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
    set -o pipefail
    install -m 0755 "${srcdir}/lxc-merebuild" "${pkgdir}/share/lxc/templates/"
    install -m 0755 "${srcdir}/autodev.hook.sh" "${pkgdir}/share/lxc/"
}

package_lxc-dev() {
    pkgfiles=(
        include
        lib/liblxc.a
        lib/pkgconfig
    )
    depends=(lxc libcap-dev)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}
