# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

rationale='The scripted build mechanism for final packages uses LXC'
pkgname=(lxc lxc-dev)
pkgver=2.1.1
pkgrel=1
pkgdesc='A userspace interface for the Linux kernel containment features'
arch=(x86_64)
url='https://linuxcontainers.org/'
license=(GPL2)
groups=(base)
depends=()
makedepends=(libcap-dev)
options=()
changelog=ChangeLog
source=(
    "https://linuxcontainers.org/downloads/lxc/lxc-${pkgver}.tar.gz"
    lxc-merebuild
)

sha256sums=(
    68663a67450a8d6734e137eac54cc7077209fb15c456eec401a2c26e6386eff6
    3442c2ffa40b6d5d083107a9e94b7e02c9eb51f04ed79fd49c4ae1c4dfd6177b
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i '/if_bridge.h/d' src/lxc/lxc_user_nic.c
    ./configure \
      --prefix='' \
      --libexecdir=/lib/lxc
    make
}

package_lxc() {
    pkgfiles=(
        bin
        etc
        lib/liblxc.so.*
        lib/lxc
        share/lxc
        var
    )
    options+=(emptydirs)
    depends=(
        acl
        libattr
        libcap
        musl
    )
    backup=(etc/lxc/default.conf)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
    set -o pipefail
    install -m 0755 "${srcdir}/lxc-merebuild" "${pkgdir}/share/lxc/templates/"
}

package_lxc-dev() {
    pkgfiles=(
        include
        lib/liblxc.so
        lib/pkgconfig
    )
    depends=(lxc libcap-dev)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}
