# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(lxc lxc-dev)
pkgver=2.0.7
pkgrel=1
pkgdesc='A userspace interface for the Linux kernel containment features'
arch=(x86_64)
url='https://linuxcontainers.org/'
license=(GPL2)
groups=(base)
depends=()
makedepends=(libcap-dev)
options=()
changelog=ChangeLog
source=(
    "https://linuxcontainers.org/downloads/lxc/lxc-${pkgver}.tar.gz"
    lxc-merebuild
)

sha256sums=(
    1c09c075f06ec029f86fa6370c7d379494ff4c66a129eda17af7b7b41e055f5d
    ecde193ce4ed90002d373e8715953e2ea1f3b61912bd5473978a67f2c940acab
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i '/if_bridge.h/d' src/lxc/lxc_user_nic.c
    ./configure \
      --prefix='' \
      --libexecdir=/lib/lxc
    make
}

package_lxc() {
    pkgfiles=(
        bin
        etc
        lib/liblxc.so.*
        lib/lxc
        share/lxc
        var
    )
    options+=(emptydirs)
    depends=(acl libcap)
    backup=(etc/lxc/default.conf)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
    set -o pipefail
    install -m 0755 "${srcdir}/lxc-merebuild" "${pkgdir}/share/lxc/templates/"
}

package_lxc-dev() {
    pkgfiles=(
        include
        lib/liblxc.so
        lib/pkgconfig
    )
    depends=(lxc libcap-dev)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
