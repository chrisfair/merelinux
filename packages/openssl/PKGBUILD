# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(openssl openssl-dev)
pkgver=1.0.2d
pkgrel=1
pkgdesc='The SSL and TLS protocols and a full-strength general purpose cryptography library.'
arch=(x86_64)
url='http://www.openssl.org'
license=(GPL2)
groups=(base)
depends=()
makedepends=(
    perl
    zlib-dev
)
options=()
changelog=ChangeLog

source=(
    "${url}/source/${pkgname}-${pkgver}.tar.gz"
)
sha256sums=(
    671c36487785628a703374c652ad2cebea45fa920ae5681515df25d9f2c9a8c8
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    sed -i '/^"linux-x86_64/s/-DTERMIO/-DTERMIOS/' Configure
    sed -i '/^"linux-elf/s/-DTERMIO/-DTERMIOS/' Configure
    sed -i 's/defined(linux)/0/' crypto/ui/ui_openssl.c
    ./config \
      --prefix=/ \
      --libdir=/lib \
      --openssldir=/etc/ssl \
      shared zlib no-dso $CFLAGS -D_HACK_DSO_BEOS
    make
}

package_openssl() {
    pkgfiles=(
        bin
        etc/ssl
        lib/engines
        "lib/libssl.so.*"
        "lib/libcrypto.so.*"
    )
    depends=(
        musl
        zlib
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make MANDIR=/share/man INSTALL_PREFIX="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_openssl-dev() {
    pkgfiles=(
        include
        "lib/*.a"
        "lib/*.so"
        lib/pkgconfig
    )
    depends=(
        openssl
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make MANDIR=/share/man INSTALL_PREFIX="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
