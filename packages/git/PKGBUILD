# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(git git-extra)
pkgver=2.19.1
pkgrel=1
pkgdesc='A distributed version control system'
arch=('x86_64')
url='http://git-scm.com/'
license=('GPL2')
groups=('base')
depends=()
makedepends=(
    libcurl-dev
    libressl-dev
    zlib-dev
    perl
)
options=()
changelog=ChangeLog

source=(
    "https://www.kernel.org/pub/software/scm/git/${pkgname}-${pkgver}.tar.xz"
)
sha256sums=(
    345056aa9b8084280b1b9fe1374d232dec05a34e8849028a20bfdb56e920dbb5
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i 's@sys/poll.h@poll.h@g' $(grep -lr 'poll.h' .)
    sed -i 's@/usr/bin/perl@/bin/perl@g' `grep -lr "/usr/bin/perl" .`
    sed -i '/absolute directory/d' configure
    LIBS='-lssl -lcrypto' ./configure \
      --prefix='' \
      --libexecdir=/lib \
      --with-curl \
      --without-tcltk \
      --with-zlib \
      --with-editor=/bin/vim \
      --with-pager=/bin/more \
      --with-perl=/bin/perl \
      --with-shell=/bin/sh
    make V=1 NO_MSGFMT=1 NO_GETTEXT=1
}

package_git() {
    pkgfiles=(
        'bin'
        'lib/git-core'
        'share/git-core'
    )
    depends=(
        libcurl
        libressl
        musl
    )
    rmfiles=(
        'bin/git-cvsserver'
        'lib/git-core/git-add--interactive'
        'lib/git-core/git-archimport'
        'lib/git-core/git-cvs*'
        'lib/git-core/git-difftool'
        'lib/git-core/git-send-email'
        'lib/git-core/git-svn'
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make V=1 NO_MSGFMT=1 NO_GETTEXT=1 DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
    cd "$pkgdir"
    find ${rmfiles[@]} -delete
}

package_git-extra() {
    pkgdesc='Extra features for git'
    pkgfiles=(
        'bin/git-cvsserver'
        'lib/git-core/git-add--interactive'
        'lib/git-core/git-archimport'
        'lib/git-core/git-cvs*'
        'lib/git-core/git-difftool'
        'lib/git-core/git-send-email'
        'lib/git-core/git-svn'
        'share/gitweb'
    )
    depends=(
        git
        libressl
        musl
        perl
    )
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}

