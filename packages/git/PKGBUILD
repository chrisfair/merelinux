pkgname=(git git-extra)
pkgver=2.10.1
pkgrel=1
pkgdesc='A distributed version control system'
arch=('x86_64')
url='http://git-scm.com/'
license=('GPL2')
groups=('base')
depends=(
    libcurl
    libressl
    zlib
)
makedepends=(
    libcurl-dev
    libressl-dev
    perl
    zlib-dev
)
options=()
changelog=ChangeLog

source=(
    "https://www.kernel.org/pub/software/scm/git/${pkgname}-${pkgver}.tar.xz"
)
sha256sums=(
    a9c32df9061cfc2c194ecaa0357ba273c1f24a0dbf0d856b03dcedafcd0a4dc6
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i 's@sys/poll.h@poll.h@g' $(grep -lr 'sys/poll.h' .)
    sed -i 's@/usr/bin/perl@/bin/perl@g' `grep -lr "/usr/bin/perl" .`
    sed -i '/absolute directory/d' configure
    ./configure \
      --prefix='' \
      --libexecdir=/lib \
      --with-curl \
      --without-tcltk \
      --with-zlib \
      --with-editor=/bin/vim \
      --with-pager=/bin/more \
      --with-perl=/bin/perl \
      --with-shell=/bin/sh
    make V=1 NO_MSGFMT=1 NO_GETTEXT=1 $MAKEFLAGS
}

package_git() {
    pkgfiles=(
        'bin'
        'lib/git-core'
        'share/git-core'
    )
    rmfiles=(
        'bin/git-cvsserver'
        'lib/git-core/git-add--interactive'
        'lib/git-core/git-archimport'
        'lib/git-core/git-cvs*'
        'lib/git-core/git-difftool'
        'lib/git-core/git-relink'
        'lib/git-core/git-send-email'
        'lib/git-core/git-svn'
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make V=1 NO_MSGFMT=1 NO_GETTEXT=1 DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${rmfiles[@]} -delete
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_git-extra() {
    depends=(
        git
        perl
    )
    pkgdesc='Extra features for git'
    pkgfiles=(
        'bin/git-cvsserver'
        'lib/git-core/git-add--interactive'
        'lib/git-core/git-archimport'
        'lib/git-core/git-cvs*'
        'lib/git-core/git-difftool'
        'lib/git-core/git-relink'
        'lib/git-core/git-send-email'
        'lib/git-core/git-svn'
        'lib/perl5'
        'share/gitweb'
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make V=1 NO_MSGFMT=1 NO_GETTEXT=1 DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

