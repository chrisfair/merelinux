pkgname=(git git-extra)
pkgver=2.5.0
pkgrel=1
pkgdesc='A distributed version control system'
arch=('x86_64')
url='http://git-scm.com/'
license=('GPL2')
groups=('base')
depends=(
    libcurl
    openssl
    zlib
)
makedepends=(
    libcurl-dev
    openssl-dev
    perl
    zlib-dev
)
options=()
changelog=ChangeLog

source=(
    "https://www.kernel.org/pub/software/scm/git/${pkgname}-${pkgver}.tar.xz"
)
sha256sums=(
    cc59b1bac6c1c67a9159872863f6c5bbe0d9404cac2a85c3e9d9fa49923ce45c
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    sed -i 's@/usr/bin/perl@/bin/perl@g' `grep -lr "/usr/bin/perl" .`
    sed -i '/absolute directory/d' configure
    ./configure \
      --prefix='' \
      --libexecdir=/lib \
      --with-curl \
      --without-tcltk \
      --with-zlib \
      --with-editor=/bin/vim \
      --with-pager=/bin/more \
      --with-perl=/bin/perl \
      --with-shell=/bin/sh
    make V=1 NO_MSGFMT=1 NO_GETTEXT=1 $MAKEFLAGS
}

package_git() {
    pkgfiles=(
        'bin'
        'lib/git-core'
        'share/git-core'
    )
    rmfiles=(
        'bin/git-cvsserver'
        'lib/git-core/git-add--interactive'
        'lib/git-core/git-archimport'
        'lib/git-core/git-cvs*'
        'lib/git-core/git-difftool'
        'lib/git-core/git-relink'
        'lib/git-core/git-send-email'
        'lib/git-core/git-svn'
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make V=1 NO_MSGFMT=1 NO_GETTEXT=1 DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${rmfiles[@]} -delete
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_git-extra() {
    depends=(
        git
        perl
    )
    pkgdesc='Extra features for git'
    pkgfiles=(
        'bin/git-cvsserver'
        'lib/git-core/git-add--interactive'
        'lib/git-core/git-archimport'
        'lib/git-core/git-cvs*'
        'lib/git-core/git-difftool'
        'lib/git-core/git-relink'
        'lib/git-core/git-send-email'
        'lib/git-core/git-svn'
        'lib/perl5'
        'share/git-web'
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make V=1 NO_MSGFMT=1 NO_GETTEXT=1 DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

