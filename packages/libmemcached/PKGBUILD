# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(libmemcached libmemcached-dev)
pkgver=1.0.18
pkgrel=1
pkgdesc='Client library for the memcached server'
arch=(x86_64)
url='http://libmemcached.org'
license=(BSD)
groups=(base)
depends=()
makedepends=(gcc-c++)
options=()
changelog=ChangeLog
source=(
    "https://launchpad.net/libmemcached/1.0/${pkgver}/+download/libmemcached-${pkgver}.tar.gz"
)

sha256sums=(
    e22c0bb032fde08f53de9ffbc5a128233041d9f33b5de022c0978a2149885f82
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    sed -i '/defined(POSIX_SPAWN_USEVFORK)/s@).*@)@' libtest/cmdline.cc
    export CFLAGS="$CFLAGS -D__USE_GNU"
    export CXXFLAGS="$CFLAGS"
    ./configure --prefix='' \
        --enable-static
    make
}

package_libmemcached() {
    pkgfiles=(
        bin
        lib/lib*.so.*
    )
    depends=(musl)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_libmemcached-dev() {
    pkgfiles=(
        include
        lib/lib*.a
        lib/lib*.so
        lib/pkgconfig
    )
    depends=(libmemcached gcc-c++)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    sed -i '/-lmemcached/s@$@ -lstdc++@' lib/pkgconfig/libmemcached.pc
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
