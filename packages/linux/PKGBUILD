# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

rationale='This is part of the core toolchain'
pkgname=(linux linux-headers)
pkgver=4.14.80
pkgrel=2
pkgdesc='System kernel'
arch=(x86_64)
url='http://www.kernel.org'
license=(GPL2)
groups=(base)
depends=()
makedepends=(bc perl)
options=()
changelog=ChangeLog
source=(
    "https://cdn.kernel.org/pub/linux/kernel/v4.x/linux-$pkgver.tar.xz"
    musl-compatibility.patch
    linux-config
)

sha256sums=(
    9ddc7bc11cbea6475ac5abf18e01a143d7d506bece591e0dcb15c9452d3ed7d2
    6ce3f62ed1553cdadd5f3963d8ac1d123b78e3af89d919a876396f7df2b56fd9
    c16b8791774cacc9e44d6ccf762e114fb5f080f36d8ed02074c7db8705fb7898
)

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    cp "${srcdir}/linux-config" .config
    make
}

package_linux() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    install -d "${pkgdir}/boot"
    install "arch/${CARCH}/boot/bzImage" "${pkgdir}/boot/vmlinux"
}

package_linux-headers() {
    pkgdesc='System headers'
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make mrproper
    patch -Np1 -i "${srcdir}/musl-compatibility.patch"
    make INSTALL_HDR_PATH=dest HOSTCFLAGS="-D_GNU_SOURCE" headers_install
    cd dest
    find include ! -type d -name "*.h" | cpio -dump "${pkgdir}"
}
