# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(linux linux-headers)
pkgver=4.10
pkgrel=1
pkgdesc='System kernel'
arch=(x86_64)
url='http://www.kernel.org'
license=(GPL2)
groups=(base)
depends=()
makedepends=(bc perl)
options=()
changelog=ChangeLog
source=(
    "https://www.kernel.org/pub/linux/kernel/v4.x/linux-$pkgver.tar.xz"
    'linux-config'
)

sha256sums=(
    3c95d9f049bd085e5c346d2c77f063b8425f191460fcd3ae9fe7e94e0477dc4b
    7ff526225976af5091ebfbb88c677ecf20cf27d7e2b76d85d9d0cbb7fa47ff05
)

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    # Something in the linux src wants gnu sed, so just be lazy for now and
    # give it what it wants
    # sudo pacman -S --force --noconfirm sed
    make mrproper
    cp "${srcdir}/linux-config" .config
    make
}

package_linux() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    install -d "${pkgdir}/boot"
    install "arch/${CARCH}/boot/bzImage" "${pkgdir}/boot/vmlinux"
}

package_linux-headers() {
    pkgdesc='System headers'
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make INSTALL_HDR_PATH=dest HOSTCFLAGS="-D_GNU_SOURCE" headers_install
    cd "${srcdir}/${pkgbase}-${pkgver}/dest"
    find include ! -type d -name "*.h" | cpio -dumpv "${pkgdir}"
}
