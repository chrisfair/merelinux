# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

rationale='This is part of the core toolchain'
pkgname=(linux linux-headers)
pkgver=4.19.34
pkgrel=1
pkgdesc='System kernel'
arch=(x86_64)
url='http://www.kernel.org'
license=(GPL2)
groups=(base)
depends=()
makedepends=(bison flex perl)
options=()
changelog=ChangeLog
source=(
    "https://cdn.kernel.org/pub/linux/kernel/v4.x/linux-$pkgver.tar.xz"
    musl-compatibility.patch
    linux-config
)

sha256sums=(
    dd795e2a1fddbee5b03c3bb55a1926829cc08df4fdcabce62dda717ba087b8cc
    fffcf736758c9eb78b7244ba7cc2860166d34c832e7c6ddc21344557a4c587a4
    5e0bca67c64c916307cf9aed132ab6e65ea939b3a1ec5c343cd0f6d1abb6563c
)

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    cp "${srcdir}/linux-config" .config
    make
}

package_linux() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    install -d "${pkgdir}/boot"
    install "arch/${CARCH}/boot/bzImage" "${pkgdir}/boot/vmlinux"
}

package_linux-headers() {
    pkgdesc='System headers'
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make mrproper
    patch -Np1 -i "${srcdir}/musl-compatibility.patch"
    make INSTALL_HDR_PATH=dest HOSTCFLAGS="-D_GNU_SOURCE" headers_install
    cd dest
    find include ! -type d -name "*.h" | cpio -dump "${pkgdir}"
}
