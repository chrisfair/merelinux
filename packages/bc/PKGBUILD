# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=bc
pkgver=1.07.1
pkgrel=1
pkgdesc='An arbitrary precision numeric processing language.'
arch=('x86_64')
url='http://www.gnu.org/software/bc/'
license=('GPL2')
groups=('base')
depends=('')
makedepends=(readline-dev bison)
options=()
changelog=ChangeLog
source=(
    "http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"
    fix-libmath_h
)

sha256sums=(
    62adfca89b0a1c0164c2cdca59ca210c1d44c3ffc46daf9931cf4942664cb02a
    cf7325777749b2dd6b4715d7c308c7c13f88f13c6ba1f3e7f08f669cd9c96833
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -m0755 "${srcdir}/fix-libmath_h" bc/fix-libmath_h
    LDFLAGS="-Wl,-static" \
    ./configure --prefix='' --with-readline \
       --mandir=/share/man \
       --infodir=/share/info
    echo "MAKEINFO = :" >> doc/Makefile
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="$pkgdir" install
    rm -rf "${pkgdir}/share/info"
}
