pkgname=(busybox busybox-static)
pkgver=1.23.2
pkgrel=1
pkgdesc='Tiny versions of common UNIX utilities built into a single binary.'
arch=(x86_64)
url='http://busybox.net'
license=(GPL)
groups=(base)
depends=()
makedepends=()
options=(!upx emptydirs)
changelog=ChangeLog
source=(
    "http://busybox.net/downloads/${pkgname}-${pkgver}.tar.bz2"
    busybox-config
    udhcpc.script
    sysctl.conf
    tty0-service
)

sha256sums=(
    05a6f9e21aad8c098e388ae77de7b2361941afa7157ef74216703395b14e319a
    368a6b813aa145fb6b4aa3fc7165aa6f542e2dfec7e168f4d143a6b1f6e7a71a
    980d3689463f75ee36ab67cff579fdcbdbeb629c99331c61c9d9397e869dd340
    0be69e4c03c50b91784dfaf9f51bceb1a01c4c61d24045c6022f2bfa6a31e4e6
    42571aa1b84d56e6cbd2091629b793ccc7e698117243911691b7310373a7e98f
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # Build busybox twice, once producing a dynamically linked binary
    # and once producting a statically linked one. The static one is
    # preferred for the final system. The dynamic one is so fakeroot
    # magic can happen while packaging, specifcally with tar.
    #
    # Fixes
    sed -i '/netinet\/ether/d' networking/arp.c
    sed -i '/net\/if_slip/d' networking/ifconfig.c
    sed -i '/getpwent/s@!.*@(pwent = getpwent()) != NULL) {@' loginutils/deluser.c
    sed -i -e '/struct passwd \*pw/d' \
      -e 's@struct passwd pwent@struct passwd \*pwent@' \
      -e 's@pwent\.pw@pwent->pw@g' loginutils/deluser.c
    cd ..
    # Static
    cp -a "${pkgname}-${pkgver}" "${pkgname}-${pkgver}-static"
    cd "${pkgname}-${pkgver}-static"
    sed "/CONFIG_PREFIX/s@=.*@=\"${pkgdir}-static/\"@" \
      "${srcdir}/busybox-config" >.config
    make V=1 HOSTCC='gcc -D_GNU_SOURCE' $MAKEFLAGS
    # Dynamic
    cd ../${pkgname}-${pkgver}
    sed "/CONFIG_PREFIX/s@=.*@=\"${pkgdir}/\"@" \
      "${srcdir}/busybox-config" >.config
    sed -i '/CONFIG_STATIC/s@.*@# CONFIG_STATIC is not set@' .config
    make V=1 HOSTCC='gcc -D_GNU_SOURCE' $MAKEFLAGS
}

package_busybox() {
    depends=(
        musl
    )
    cd "${pkgbase}-${pkgver}"
    make V=1 HOSTCC="gcc -D_GNU_SOURCE" install
    cd "$pkgdir"
    chmod u+s bin/busybox
    rm -f bin/bash
    install -d etc/network/if-pre-up.d
    install -d etc/network/if-up.d
    install -d etc/network/if-down.d
    install -d etc/network/if-post-down.d
    install -d share/udhcpc
    install -m 0755 "${srcdir}/udhcpc.script" \
        share/udhcpc/default.script
    install -m 0644 "${srcdir}/sysctl.conf" etc/
    install -d etc/s6-services/tty0
    install -m 0754 "${srcdir}/tty0-service" \
        etc/s6-services/tty0/run
}

package_busybox-static() {
    cd "${pkgbase}-${pkgver}-static"
    make V=1 HOSTCC="gcc -D_GNU_SOURCE" install
    cd "$pkgdir"
    chmod u+s bin/busybox
    rm -f bin/bash
    install -d etc/network/if-pre-up.d
    install -d etc/network/if-up.d
    install -d etc/network/if-down.d
    install -d etc/network/if-post-down.d
    install -d share/udhcpc
    install -m 0755 "${srcdir}/udhcpc.script" \
        share/udhcpc/default.script
    install -m 0644 "${srcdir}/sysctl.conf" etc/
    install -d etc/s6-services/tty0
    install -m 0754 "${srcdir}/tty0-service" \
        etc/s6-services/tty0/run
}
