# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(gmp gmp-dev)
pkgver=6.1.1
pkgrel=1
pkgdesc='A free library for arbitrary precision arithmetic.'
arch=('x86_64')
url='http://gmplib.org'
license=(LGPL BSD)
groups=(base)
depends=()
makedepends=(gcc-c++)
options=()
changelog=ChangeLog
source=(
    "https://gmplib.org/download/gmp/${pkgname}-${pkgver}.tar.xz"
)
sha256sums=(
    d36e9c05df488ad630fff17edb50051d6432357f9ce04e34a09b3d818825e831
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure --prefix='' \
      --enable-cxx \
      --target=${arch}-unknown-linux-gnu \
      --host=${arch}-unknown-linux-gnu \
      --build=${arch}-unknown-linux-gnu
    make $MAKEFLAGS
}

package_gmp() {
    pkgfiles=(
        lib/libgmp.so.*
        lib/libgmpxx.so.*
    )
    depends=(musl)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -pudmv "${pkgdir}"
}

package_gmp-dev() {
    pkgfiles=(
        include
        lib/libgmp.so
        lib/libgmp.a
        lib/libgmpxx.so
        lib/libgmpxx.a
    )
    depends=(gmp)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -pudmv "${pkgdir}"
}
