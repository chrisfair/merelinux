# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(file file-dev)
pkgver=5.30
pkgrel=1
pkgdesc='A file-type guesser'
arch=('x86_64')
url='http://darwinsys.com/file/'
license=('BSD')
groups=('base')
depends=()
makedepends=()
options=()
changelog=ChangeLog
source=(
    "ftp://ftp.astron.com/pub/file/${pkgname}-${pkgver}.tar.gz"
)

sha256sums=(
    694c2432e5240187524c9e7cf1ec6acc77b47a0e19554d34c14773e43dbbf214
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    sed -i 's/misc/file/' configure
    LDFLAGS='-Wl,-static' ./configure --prefix='' \
      --disable-shared \
      --enable-static
    make V=1
}

package_file() {
    options=()
    pkgfiles=(
        bin/file
        share/file/magic.mgc
    )
    cd "$srcdir/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_file-dev() {
    pkgfiles=(
        include
        lib
    )
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
