# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

rationale='LXC requires libcap'
pkgname=(libcap libcap-dev)
pkgver=2.25
pkgrel=1
pkgdesc='A Linux capability implementation.'
arch=(x86_64)
url='https://sites.google.com/site/fullycapable/'
license=(GPL)
groups=(base)
depends=()
makedepends=(
    libattr-dev
    perl
)
options=()
changelog=ChangeLog
source=(
    "https://www.kernel.org/pub/linux/libs/security/linux-privs/libcap2/libcap-${pkgver}.tar.xz"
)
sha256sums=(
    693c8ac51e983ee678205571ef272439d83afe62dd8e424ea14ad9790bc35162
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    sed -i '/^lib=/s@=.*@=/lib@' Make.Rules
    make prefix=/
}

package_libcap() {
    pkgfiles=(
        "lib/libcap.so.*"
        sbin
    )
    depends=(
        libattr
        musl
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make RAISE_SETFCAP=no prefix=/ DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_libcap-dev() {
    pkgfiles=(
        include
        lib/libcap.a
        lib/libcap.so
        lib/pkgconfig
    )
    depends=(libcap)

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make RAISE_SETFCAP=no prefix=/ DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
