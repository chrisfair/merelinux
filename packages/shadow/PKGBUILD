# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=shadow
pkgver=4.4
pkgrel=1
pkgdesc='Miscellaneous login utilities for Linux'
arch=(x86_64)
url=https://github.com/shadow-maint/shadow
license=(GPL)
groups=(base)
depends=()
makedepends=()
options=()
changelog=ChangeLog
source=(
    "https://github.com/shadow-maint/shadow/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz"
    useradd.patch
)

sha256sums=(
    61a9a4041b552eab9a253788d589e3664cf9b903bdd1f1ca73ea8aac4a71f03c
    c8d3efc2464f89a22623d25542db5aa171139c57c3800bb98c89e4cc6ac9537d
)

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -Np1 -i "${srcdir}/useradd.patch"
    sed -i 's/groups$(EXEEXT) //' src/Makefile.in
    sed -i -e 's@#ENCRYPT_METHOD DES@ENCRYPT_METHOD SHA512@' etc/login.defs
    sed -i 's/1000/999/' etc/useradd
    sed -i -e '47 d' -e '60,65 d' libmisc/myname.c
    sed -i '/sgroupio.h/a #include "getdef.h"' lib/sgroupio.c
    sed -i '/shadowio.h/a #include "getdef.h"' lib/shadowio.c
    CFLAGS+=' -fPIC' LDFLAGS='-Wl,-static' \
        ./configure --prefix='' \
        --sysconfdir=/etc \
        --with-group-name-max-length=32
    make
}

package() {
    pkgfiles=(
        bin
        etc
        sbin
        share/man
    )
    depends=()
    cd "${srcdir}/${pkgname}-${pkgver}"
    MAKEFLAGS='' make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    rm bin/su
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
