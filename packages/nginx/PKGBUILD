pkgname=nginx
pkgver=1.7.11
pkgrel=1
pkgdesc='An HTTP and reverse proxy server.'
arch=(x86_64)
url='http://nginx.org/'
license=(BSD)
groups=(base)
depends=(s6 execline)
makedepends=(
    pkgconf
    libpcre-devel
    openssl-devel
    zlib-devel
)
options=()
changelog=ChangeLog
source=(
    "http://nginx.org/download/nginx-${pkgver}.tar.gz"
    nginx-service
    nginx-log
    nginx.install
)

sha256sums=(
    dad9d740210e638bfd480536910083ed13f04c04775eedf877984e1c61a69695
    3a62b6f4cf05a308f66214e09e8b64dde4a1fe04a78ec0e3cdecc927cf882fae
    14da3ca97bcfe51299f007119350b3c318a5158a1e6697cbf23be909f982fd21
    d13a45a59449015fe4e855baadf438b7223c623f58427092ae23facccbceb6fd
)

install=nginx.install


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    CC="gcc -fPIC -static" \
        ./configure \
        --prefix='' \
        --conf-path=/etc/nginx/nginx.conf \
        --pid-path=/var/run/nginx.pid \
        --lock-path=/var/lock/nginx.lock \
        --http-log-path=/var/log/nginx/access.log \
        --error-log-path=/var/log/nginx/error.log \
        --http-client-body-temp-path=/var/tmp/nginx/client_body \
        --http-proxy-temp-path=/var/tmp/nginx/proxy \
        --http-fastcgi-temp-path=/var/tmp/nginx/fastcgi \
        --http-uwsgi-temp-path=/var/tmp/nginx/uwsgi \
        --http-scgi-temp-path=/var/tmp/nginx/scgi \
        --user=nginx \
        --group=nogroup \
        --with-threads
    make $MAKEFLAGS
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="$pkgdir" install
    install -d "${pkgdir}"/share/nginx
    mv "${pkgdir}"/html "${pkgdir}"/share/nginx/
    sed -i '/root.*html/s@html@/share/nginx/html@' \
        "${pkgdir}"/etc/nginx/nginx.conf
    install -d "${pkgdir}/etc/s6-services/nginx/log"
    install -m 0754 "${srcdir}/nginx-service" \
        "${pkgdir}/etc/s6-services/nginx/run"
    install -m 0754 "${srcdir}/nginx-log" \
        "${pkgdir}/etc/s6-services/nginx/log/run"
    find "$pkgdir"/etc -name "*.default" -delete
}
