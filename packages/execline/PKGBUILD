# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(execline execline-dev)
pkgver=2.3.0.0
pkgrel=1
pkgdesc='a (non-interactive) scripting language.'
arch=(x86_64)
url='http://skarnet.org/software/execline/'
license=(ISC)
groups=(base)
depends=()
makedepends=(skalibs-dev)
options=()
changelog=ChangeLog
source=(
    "http://skarnet.org/software/execline/execline-${pkgver}.tar.gz"
)

sha256sums=(
    a0ec43b8feba299cc1e5c65b1978ed76571afa595bc53165373e29a57468f425
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i 's@#!/bin/sh@#!/bin/bash@' configure
    ./configure \
      --prefix=/ \
      --enable-static-libc
    make
}

package_execline() {
    pkgfiles=(
        bin
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_execline-dev() {
    pkgfiles=(
        include
        lib
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    mv lib/execline/libexecline.a lib
    rm -rf lib/execline
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
