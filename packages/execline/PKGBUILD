# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(execline execline-dev)
pkgver=2.5.1.0
pkgrel=1
pkgdesc='a (non-interactive) scripting language.'
arch=(x86_64)
url='http://skarnet.org/software/execline/'
license=(ISC)
groups=(base)
depends=()
makedepends=(skalibs-dev)
options=()
changelog=ChangeLog
source=(
    "http://skarnet.org/software/execline/execline-${pkgver}.tar.gz"
)

sha256sums=(
    b1a756842947488404db8173bbae179d6e78b6ef551ec683acca540ecaf22677
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i 's@#!/bin/sh@#!/bin/bash@' configure
    ./configure \
      --prefix=/ \
      --enable-static-libc
    make
}

package_execline() {
    pkgfiles=(
        bin
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    mv lib/execline/libexecline.a lib
    rm -rf lib/execline
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_execline-dev() {
    pkgfiles=(
        include
        lib
    )
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
