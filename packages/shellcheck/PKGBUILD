# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=shellcheck
pkgver=0.5.0
pkgrel=1
pkgdesc='A static analysis tool for shell scripts'
arch=('x86_64')
url='https://github.com/koalaman/shellcheck'
license=('GPL3')
groups=('base')
depends=()
makedepends=(
    cabal-install
    ghc
    gmp-dev
)
options=()
changelog=ChangeLog
source=(
    "https://github.com/koalaman/shellcheck/archive/v${pkgver}.tar.gz"
)

sha256sums=(
    348a3f7892c1f28a44f188c00ac82f1b3bf899d9f81d14ddb0e306db26c937bb
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i "/^executable/s@.*@&\n    ld-options:\n      -static@" ShellCheck.cabal
    cabal sandbox init --sandbox="${srcdir}/destdir"
    cabal update
    cabal install
}

package() {
    pkgfiles=(
        bin
    )
    cd "${srcdir}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
