# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(libgcrypt libgcrypt-dev)
pkgver=1.7.6
pkgrel=1
pkgdesc='A general purpose cryptographic library based on the code from GnuPG'
arch=(x86_64)
url='https://www.gnupg.org/'
license=(GPL3)
groups=(base)
depends=()
makedepends=(libgpg-error-dev)
options=()
changelog=ChangeLog
source=(
    "https://www.gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
)

sha256sums=(
    626aafee84af9d2ce253d2c143dc1c0902dda045780cc241f39970fc60be05bc
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    CFLAGS+=' -fPIC' ./configure --prefix='' \
        --enable-static
    make
}

package_libgcrypt() {
    pkgfiles=(
        bin
        lib/libgcrypt.so.*
    )
    depends=(libgpg-error musl)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
    rm "${pkgdir}/bin/libgcrypt-config"
}

package_libgcrypt-dev() {
    pkgfiles=(
        bin/libgcrypt-config
        include
        lib/libgcrypt.a
        lib/libgcrypt.so
        share/aclocal
    )
    depends=(libgcrypt libgpg-error-dev)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}
