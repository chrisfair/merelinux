# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(gnutls gnutls-dev)
pkgver=3.5.11
pkgrel=1
pkgdesc='A secure communications library implementing the SSL, TLS and DTLS protocols'
arch=(x86_64)
url='http://www.gnutls.org/'
license=(GPL3)
groups=(base)
depends=()
makedepends=(
    gmp-dev
    nettle-dev
    zlib-dev
)
options=()
changelog=ChangeLog
source=(
    "http://gd.tuwien.ac.at/privacy/gnupg/gnutls/v3.5/gnutls-${pkgver}.tar.xz"
)

sha256sums=(
    51765cc5579e250da77fbd7871507c517d01b15353cc40af7b67e9ec7b6fe28f
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    CFLAGS+=' -fPIC' ./configure --prefix='' \
        --with-included-libtasn1 \
        --with-included-unistring \
        --without-p11-kit \
        --enable-static
    make
}

package_gnutls() {
    pkgfiles=(
        bin
        lib/libgnutls.so.*
    )
    depends=(musl nettle gmp)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    sed -i 's@-lgnutls@& -lhogweed -lgmp -lnettle -lz@' lib/pkgconfig/gnutls.pc
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}

package_gnutls-dev() {
    pkgfiles=(
        include
        lib/libgnutls.a
        lib/libgnutls.so
        lib/pkgconfig
    )
    depends=(gnutls gmp-dev nettle-dev zlib-dev)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}
