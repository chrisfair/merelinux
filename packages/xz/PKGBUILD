# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(xz liblzma-dev)
pkgver=5.2.3
pkgrel=1
pkgdesc='Free general-purpose data compression software with high compression ratio.'
arch=('x86_64')
url='http://tukaani.org/xz/'
license=('GPL2')
groups=('base')
makedepends=()
options=()
changelog=ChangeLog

source=(
    "${url}/${pkgname}-${pkgver}.tar.xz"
)
sha256sums=(
    7876096b053ad598c31f6df35f7de5cd9ff2ba3162e5a5554e4fc198447e0347
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    CFLAGS='-fPIC' LDFLAGS='-Wl,-static' \
      ./configure \
      --prefix='' \
      --disable-shared \
      --enable-static \
      --disable-nls \
      --includedir=/include \
      --mandir=/share/man
    make
}

package_xz() {
    pkgfiles=(
        bin/*
    )
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    # The following files are provided in busybox
    rm bin/{lzcat,unlzma,unxz,xzcat}
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_liblzma-dev() {
    pkgfiles=(
        include
        lib/liblzma.a
        lib/pkgconfig/liblzma.pc
    )
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
