# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

rationale='Busybox provides an xz utility, however libarchive and pacman will use the liblzma library that ships with xz'
pkgname=(xz liblzma-dev)
pkgver=5.2.4
pkgrel=1
pkgdesc='Free general-purpose data compression software with high compression ratio.'
arch=('x86_64')
url='http://tukaani.org/xz'
license=('GPL2')
groups=('base')
makedepends=()
options=()
changelog=ChangeLog

source=(
    "${url}/${pkgname}-${pkgver}.tar.xz"
)
sha256sums=(
    9717ae363760dedf573dad241420c5fea86256b65bc21d2cf71b2b12f0544f4b
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    CFLAGS='-fPIC' LDFLAGS='-Wl,-static' \
      ./configure \
      --prefix='' \
      --disable-shared \
      --enable-static \
      --disable-nls \
      --includedir=/include \
      --mandir=/share/man
    make
}

package_xz() {
    pkgfiles=(
        bin/*
    )
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    # The following files are provided in busybox
    rm bin/{lzcat,unlzma,unxz,xzcat}
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}

package_liblzma-dev() {
    pkgfiles=(
        include
        lib/liblzma.a
        lib/pkgconfig/liblzma.pc
    )
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}
