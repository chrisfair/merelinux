# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

rationale='curl requires an ssl library for https functionality'
pkgname=(libressl libressl-dev ca-certs)
pkgver=2.8.2
pkgrel=1
pkgdesc='A fork of OpenSSL 1.0.1g with the goal modernizing the codebase and improving security'
arch=(x86_64)
url='http://www.libressl.org'
license=(OpenSSL SSLeay)
groups=(base)
depends=()
makedepends=(libtool)
options=()
changelog=ChangeLog

source=(
    "http://ftp.openbsd.org/pub/OpenBSD/LibreSSL/libressl-${pkgver}.tar.gz"
)
sha256sums=(
    b8cb31e59f1294557bfc80f2a662969bc064e83006ceef0574e2553a1c254fd5
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    autoreconf -i
    CFLAGS='-fPIC' ./configure --prefix=''
    make V=1
}

check() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make check
    make DESTDIR="${pkgdirbase}/dest" install
}

package_libressl() {
    pkgfiles=(
        bin
        etc/ssl/*.cnf
        "lib/lib*.so.*"
    )
    depends=(musl)

    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}

package_libressl-dev() {
    pkgfiles=(
        include
        "lib/*.a"
        "lib/*.so"
        lib/pkgconfig
    )
    depends=(libressl)

    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}

package_ca-certs() {
    pkgfiles=(
        etc/ssl/ca-certs.pem
    )
    cd "${pkgdirbase}/dest"
    mv etc/ssl/cert.pem etc/ssl/ca-certs.pem
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}
