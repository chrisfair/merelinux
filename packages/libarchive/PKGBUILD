pkgname=('libarchive' 'libarchive-utils' 'libarchive-devel')
pkgver=3.1.2
pkgrel=2
pkgdesc='Multi-format archive and compression library'
arch=('x86_64')
url='http://www.libarchive.org'
license=('BSD')
groups=('base')
depends=('')
makedepends=(
	'musl-devel'
	'binutils'
	'gcc'
	'linux-headers'
	'openssl-devel'
	'zlib-devel'
)
options=()
changelog=ChangeLog
source=(
	"http://www.libarchive.org/downloads/${pkgname}-${pkgver}.tar.gz"
	'libarchive-ENOMEM_fix.patch'
)
sha256sums=(
	'eb87eacd8fe49e8d90c8fdc189813023ccc319c5e752b01fb6ad0cc7b2c53d5e'
	'bbea265ab0191c734dab47c23c543584757c0a3f55619c5afba5a0f9a3550a33'
)


build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	# fix for musl
	sed -i 's@HAVE_LCHMOD@&_DISABLE@' libarchive/archive_write_disk_posix.c
	# fix for busybox xz
	sed -i 's@ -qq@@' libarchive/archive_read_support_filter_xz.c
	# upstream fix for .MTREE creation
	patch -Np1 -i ../libarchive-ENOMEM_fix.patch
	LDFLAGS='--static' \
	./configure \
	  --prefix=/ \
	  --enable-static \
	  --disable-shared
	make V=1 $MAKEFLAGS
	install -v bsdtar{,-static}
	install -v bsdcpio{,-static}
	unset LDFLAGS
	make clean
	./configure \
	  --prefix=/ \
	  --enable-static \
	  --enable-shared
	make V=1 $MAKEFLAGS
}

package_libarchive() {
	pkgfiles=(
		'lib/libarchive.so.13'
		"lib/libarchive.so.1${pkgver}"
	)
	depends=(
		'openssl'
		'zlib'
	)
	
	cd "${srcdir}/${pkgbase}-${pkgver}"
	make DESTDIR="${pkgdir}-tmp" install
	cd "${pkgdir}-tmp"
	printf "%s\n" ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_libarchive-devel() {
	depends=("$pkgbase")
	pkgfiles=(
		'include/*'
		'lib/libarchive.a'
		'lib/libarchive.so'
		'lib/pkgconfig/*'
		'share/man/man3/*'
	)
	depends=("$pkgbase")
	
	cd "${srcdir}/${pkgbase}-${pkgver}"
	make DESTDIR="${pkgdir}-tmp" install
	cd "${pkgdir}-tmp"
	printf "%s\n" ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_libarchive-utils() {
	pkgfiles=(
		'bin/bsdcpio'
		'bin/bsdtar'
		'share/man/man1/*'
	)
	
	cd "$srcdir/$pkgbase-$pkgver"
	make DESTDIR="${pkgdir}-tmp" install
	install -v -m 0755 bsdtar-static "${pkgdir}-tmp/bin/bsdtar" 
	install -v -m 0755 bsdcpio-static "${pkgdir}-tmp/bin/bsdcpio" 
	cd "${pkgdir}-tmp"
	printf "%s\n" ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

