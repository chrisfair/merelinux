# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

rationale='libarchive is a core dependency of pacman'
pkgname=(
    libarchive
    libarchive-dev
)
pkgver=3.3.3
pkgrel=1
pkgdesc='Multi-format archive and compression library'
arch=(x86_64)
url='http://www.libarchive.org'
license=(BSD)
groups=(base)
depends=()
makedepends=(
    libacl-dev
    libattr-dev
    liblzma-dev
    nettle-dev
    zlib-dev
)
options=()
changelog=ChangeLog
source=(
    "http://www.libarchive.org/downloads/libarchive-${pkgver}.tar.gz"
)
sha256sums=(
    ba7eb1781c9fbbae178c4c6bad1c6eb08edab9a1496c64833d1715d022b30e2e
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    # fix for musl
    sed -i 's@HAVE_LCHMOD@&_DISABLE@' libarchive/archive_write_disk_posix.c
    # fixes for busybox xz
    sed -i 's@ -qq@@' libarchive/archive_read_support_filter_xz.c
    sed -i 's@xz -d@unxz@' libarchive/archive_read_support_filter_xz.c
    sed -i 's@lzma -d@unlzma@' libarchive/archive_read_support_filter_xz.c
    ./configure \
      --prefix='' \
      --enable-static \
      --enable-shared
    make V=1
}

package_libarchive() {
    pkgfiles=(
        bin
        lib/*.so.*
        share/man/man1
        share/man/man5/libarchive-formats*
    )
    depends=(
        libacl
        libattr
        musl
        nettle
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/destdir" install

    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}

package_libarchive-dev() {
    depends=(libarchive)
    pkgfiles=(
        include
        lib/*.a
        lib/*.so
        lib/pkgconfig
        share/man/man3
    )
    depends=(libarchive)

    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}
