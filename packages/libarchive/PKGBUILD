pkgname=(
    libarchive
    libarchive-dev
)
pkgver=3.2.1
pkgrel=1
pkgdesc='Multi-format archive and compression library'
arch=(x86_64)
url='http://www.libarchive.org'
license=(BSD)
groups=(base)
depends=()
makedepends=(
    libacl-dev
    libattr-dev
    liblzma-dev
    libressl-dev
    zlib-dev
)
options=(!upx)
changelog=ChangeLog
source=(
    "http://www.libarchive.org/downloads/${pkgname}-${pkgver}.tar.gz"
)
sha256sums=(
    72ee1a4e3fd534525f13a0ba1aa7b05b203d186e0c6072a8a4738649d0b3cfd2
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    # fix for musl
    sed -i 's@HAVE_LCHMOD@&_DISABLE@' libarchive/archive_write_disk_posix.c
    # fixes for busybox xz
    sed -i 's@ -qq@@' libarchive/archive_read_support_filter_xz.c
    sed -i 's@xz -d@unxz@' libarchive/archive_read_support_filter_xz.c
    sed -i 's@lzma -d@unlzma@' libarchive/archive_read_support_filter_xz.c
    CFLAGS='-fPIC' \
      ./configure \
      --prefix='' \
      --enable-static \
      --enable-shared
    make V=1 $MAKEFLAGS
}

check() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="$(pwd)/.destdir" install
}

package_libarchive() {
    pkgfiles=(
        bin
        "lib/libarchive.so.*"
    )
    depends=(
        libacl
        libattr
        musl
        libressl
        zlib
    )
    cd "${srcdir}/${pkgbase}-${pkgver}/.destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}

package_libarchive-dev() {
    depends=(libarchive)
    pkgfiles=(
        include
        lib/libarchive.a
        lib/libarchive.so
        lib/pkgconfig
    )
    depends=(libarchive)
    cd "${srcdir}/${pkgbase}-${pkgver}/.destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "${pkgdir}"
}
