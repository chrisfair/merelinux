pkgname=(mandoc mandoc-dev)
pkgver=1.13.4
pkgrel=1
pkgdesc='UNIX manpage tools'
arch=(x86_64)
url='http://mdocml.bsd.lv/'
license=(ISC)
groups=(base)
depends=()
makedepends=(libsqlite-dev libz-dev)
options=()
changelog=ChangeLog
source=(
    "http://mdocml.bsd.lv/snapshots/mdocml-${pkgver}.tar.gz"
)

sha256sums=(
    0a55c1addb188071d6f784599303656b8465e98ec6b2f4f264e12fb96d79e0ef
)


build() {
    cd "${srcdir}/mdocml-${pkgver}"
    echo "LDFLAGS='-Wl,--static'" >configure.local
    echo "PREFIX=''" >>configure.local
    echo "MANDIR='/share/man'" >>configure.local
    ./configure
    make
}

package_mandoc() {
    pkgfiles=(
        bin
        sbin
        share/man/man1
        share/man/man5
        share/man/man7
        share/man/man8
    )
    options=(upx)
    cd "${srcdir}/mdocml-${pkgver}"
    MAKEFLAGS='' make DESTDIR="${pkgdirbase}/destdir" install
    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_mandoc-dev() {
    pkgfiles=(
        include
        lib
        share/man/man3
    )
    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
