# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(e2fsprogs libblkid-dev libuuid-dev)
pkgver=1.43.4
pkgrel=1
pkgdesc='Filesystem utilities for ext2, ext3 and ext4.'
arch=(x86_64)
url=http://e2fsprogs.sourceforge.net/
license=(GPL)
groups=(base)
depends=()
makedepends=(libattr-dev gettext libtool)
options=()
changelog=ChangeLog
source=(
    "https://www.kernel.org/pub/linux/kernel/people/tytso/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.xz"
)

sha256sums=(
    54b3f21123a531a6a536b9cdcc21344b0122a72790dbe4dacc98e64db25e4a24
)

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    autoreconf -i
    mkdir build
    cd build
    CFLAGS+=' -fPIC' LDFLAGS='-Wl,-static' \
      ../configure --prefix=''
    MAKEFLAGS='' make V=1
}

package_e2fsprogs() {
    pkgfiles=(
        bin
        etc/mke2fs.conf
        sbin
        share/man/man1
        share/man/man5
        share/man/man8
    )
    cd "${srcdir}/${pkgbase}-${pkgver}/build"
    MAKEFLAGS='' make DESTDIR="${pkgdirbase}/dest" install install-libs
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_libblkid-dev() {
    pkgfiles=(
        include/blkid
        lib/pkgconfig/blkid.pc
        lib/libblkid.a
    )
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libuuid-dev() {
    pkgfiles=(
        include/uuid
        lib/pkgconfig/uuid.pc
        lib/libuuid.a
    )
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
