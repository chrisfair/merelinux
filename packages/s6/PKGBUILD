pkgname=(s6 s6-devel)
pkgver=2.1.3.0
pkgrel=2
pkgdesc='A small process supervision suite for UNIX.'
arch=(x86_64)
url='http://skarnet.org/software/s6/'
license=(ISC)
groups=(base)
depends=()
makedepends=(skalibs-devel execline-devel)
options=()
changelog=ChangeLog
source=(
    "http://skarnet.org/software/s6/s6-${pkgver}.tar.gz"
    init
    tty0-service
)

sha256sums=(
    73057df188f1ac8db186b96298c0cb6081199aa73d3f2f8d5fabdbbe21c3ea04
    c399575e649a93a83db6fcf5795568bc97db5c37d8ac2fe442764a58a125d538
    42571aa1b84d56e6cbd2091629b793ccc7e698117243911691b7310373a7e98f
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
      --prefix=/ \
      --libexecdir=/lib/s6 \
      --enable-static-libc
    make $MAKEFLAGS
}

package_s6() {
    options+=(emptydirs)
    pkgfiles=(
        etc/services
        service
        sbin
        lib/s6
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    rm lib/s6/libs6.a
    mv bin/* sbin/
    install -m 0750 "${srcdir}/init" sbin/init
    install -d etc/services/tty0
    install -m 0754 "${srcdir}/tty0-service" etc/services/tty0/run
    install -d service
    ln -s ../etc/services/tty0 service/tty0
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_s6-devel() {
    pkgfiles=(
        include
        lib/libs6.a
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    mv lib/s6/libs6.a lib/
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
