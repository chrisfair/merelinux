pkgname=(s6 s6-devel)
pkgver=2.1.3.0
pkgrel=2
pkgdesc='A small process supervision suite for UNIX.'
arch=(x86_64)
url='http://skarnet.org/software/s6/'
license=(ISC)
groups=(base)
depends=()
makedepends=(skalibs-devel execline-devel)
options=()
changelog=ChangeLog
source=(
    "http://skarnet.org/software/s6/s6-${pkgver}.tar.gz"
    init
    service
    service_functions.sh
)

sha256sums=(
    73057df188f1ac8db186b96298c0cb6081199aa73d3f2f8d5fabdbbe21c3ea04
    c399575e649a93a83db6fcf5795568bc97db5c37d8ac2fe442764a58a125d538
    611d31bf4351ddc40867ef9507c130f8de5f2042c68b4642b2cb31957cbc8f24
    6871693d9834adc79aea3ce7989b028279533dc7e7063cf8ef2182f2afe2cfd2
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
      --prefix=/ \
      --libexecdir=/lib/s6 \
      --enable-static-libc
    make $MAKEFLAGS
}

package_s6() {
    options+=(emptydirs)
    pkgfiles=(
        etc/s6-services
        service
        sbin
        lib/s6
    )
    depends=(skalibs-leapsecs)

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    rm lib/s6/libs6.a
    mv bin/* sbin/
    install -m 0750 "${srcdir}/init" sbin/init
    install -d service
    install -d etc/s6-services
    install -m 0754 "${srcdir}/service_functions.sh" lib/s6/
    install -m 0754 "${srcdir}/service" sbin/
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_s6-devel() {
    pkgfiles=(
        include
        lib/libs6.a
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}-tmp" install
    cd "${pkgdir}-tmp"
    mv lib/s6/libs6.a lib/
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
