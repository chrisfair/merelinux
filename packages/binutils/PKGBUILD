# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

# FIXME: Stick with 2.30 until 2.32 is released
note='Version 2.31 includes a bug that causes static binaries to segfault. See: https://sourceware.org/bugzilla/show_bug.cgi?id=23428. Version 2.32 will contain the fix. For now, stick with 2.30.'
rationale='This is part of the core toolchain'
pkgname=binutils
pkgver=2.30
pkgrel=1
pkgdesc='A collection of binary tools, including a linker and an assembler'
arch=('x86_64')
url='http://www.gnu.org/software/binutils/'
license=('GPL3')
groups=('base')
depends=(musl)
makedepends=(zlib-dev)
options=()
changelog=ChangeLog
source=(
    "http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz"
)

sha256sums=(
    6e46b8aeae2f727a36f0bd9505e405768a72218f1796f0d09757d45209871ae6
)



build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    mkdir build
    cd build
    ../configure --prefix='' \
      --build=${CHOST} \
      --host=${CHOST} \
      --target=${CHOST} \
      --enable-gold \
      --enable-gold=default \
      --enable-plugins \
      --enable-shared \
      --disable-werror \
      --disable-nls \
      --with-system-zlib \
      --mandir=/share/man
    make tooldir=/
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"
    #MAKEFLAGS='' make LDFLAGS='' check
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"
    make DESTDIR="$pkgdir" tooldir=/ install
    rm -rf "${pkgdir}/info"
}
