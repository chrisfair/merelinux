# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=binutils
pkgver=2.29
pkgrel=1
pkgdesc='A collection of binary tools, including a linker and an assembler'
arch=('x86_64')
url='http://www.gnu.org/software/binutils/'
license=('GPL3')
groups=('base')
depends=(musl)
makedepends=(bison zlib-dev)
options=()
changelog=ChangeLog
source=(
    "http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
)

sha256sums=(
    29a29549869039aad75fdf507ac30366da5ad0b974fbff4a8e7148dbf4f40ebf
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    mkdir build
    cd build
    ../configure --prefix='' \
      --build=${CHOST} \
      --host=${CHOST} \
      --target=${CHOST} \
      --enable-gold \
      --enable-ld=default \
      --enable-plugins \
      --enable-shared \
      --disable-werror \
      --disable-nls \
      --with-system-zlib \
      --mandir=/share/man
    make tooldir=/
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"
    # MAKEFLAGS='' make LDFLAGS='' check
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"
    make DESTDIR="$pkgdir" tooldir=/ install
    rm -rf "${pkgdir}/info"
}
