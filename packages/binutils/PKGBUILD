# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=binutils
pkgver=2.28
pkgrel=1
pkgdesc='A collection of binary tools, including a linker and an assembler'
arch=('x86_64')
url='http://www.gnu.org/software/binutils/'
license=('GPL3')
groups=('base')
depends=(musl)
makedepends=(bison zlib-dev)
options=()
changelog=ChangeLog
source=(
    "http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
)

sha256sums=(
    6297433ee120b11b4b0a1c8f3512d7d73501753142ab9e2daa13c5a3edd32a72
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    mkdir build
    cd build
    ../configure --prefix='' \
      --build=${CHOST} \
      --host=${CHOST} \
      --target=${CHOST} \
      --enable-gold \
      --enable-ld=default \
      --enable-plugins \
      --enable-shared \
      --disable-werror \
      --disable-nls \
      --with-system-zlib \
      --mandir=/share/man
    make tooldir=/
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"
    MAKEFLAGS='' make LDFLAGS='' check
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"
    make DESTDIR="$pkgdir" tooldir=/ install
    rm -rf "${pkgdir}/info"
}
