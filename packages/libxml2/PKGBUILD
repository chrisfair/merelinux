# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(libxml2 libxml2-dev)
pkgver=2.9.4
pkgrel=2
pkgdesc='An XML C parser and toolkit.'
arch=(x86_64)
url='http://xmlsoft.org/'
license=(GPL)
groups=(base)
depends=()
makedepends=(zlib-dev)
options=()
changelog=ChangeLog
source=(
    "ftp://xmlsoft.org/libxml2/libxml2-${pkgver}.tar.gz"
)
sha256sums=(
    ffb911191e509b966deb55de705387f14156e1a56b21824357cdf0053233633c
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    CFLAGS+=' -fPIC -D__GNUC__=2' \
      ./configure --prefix='' \
      --enable-static \
      --enable-shared
    make V=1
}

package_libxml2() {
    pkgfiles=(
        bin/xmlcatalog
        bin/xmllint
        lib/*.so.*
        share/man/man1
    )
    depends=(musl)

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}

package_libxml2-dev() {
    pkgfiles=(
        bin/xml2-config
        include
        lib/*.a
        lib/*.so
        lib/cmake
        lib/xml*.sh
        lib/pkgconfig
        share/man/man3
    )
    depends=(libxml2)

    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "$pkgdir"
}
