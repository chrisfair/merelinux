# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(libxml2 libxml2-dev)
pkgver=2.9.4
pkgrel=1
pkgdesc='An XML C parser and toolkit.'
arch=(x86_64)
url='http://xmlsoft.org/'
license=(GPL)
groups=(base)
depends=()
makedepends=(
    libz-dev
)
options=()
changelog=ChangeLog
source=(
    "ftp://xmlsoft.org/libxml2/libxml2-${pkgver}.tar.gz"
)
sha256sums=(
    ffb911191e509b966deb55de705387f14156e1a56b21824357cdf0053233633c
)


build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    CFLAGS="$CFLAGS -fPIC -D__GNUC__=2" \
      ./configure \
      --prefix='' \
      --enable-static \
      --enable-shared
    make V=1
}

package_libxml2() {
    pkgfiles=(
        lib/*.so.*
    )
    depends=(
        musl
    )

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/destdir" install
    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}

package_libxml2-dev() {
    pkgfiles=(
        bin/xml2-config
        include
        lib/*.a
        lib/*.so
    )
    depends=(libxml2)

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/destdir" install
    cd "${pkgdirbase}/destdir"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dumpv "$pkgdir"
}
