# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(flex flex-dev)
pkgver=2.6.4
pkgrel=1
pkgdesc='A tool for generating scanners, or tokenizers that recognize lexical patterns in text.'
arch=('x86_64')
url='http://flex.sourceforge.net/'
license=('GPL2')
groups=('base')
depends=()
makedepends=(bison)
options=()
changelog=ChangeLog
source=(
    "http://github.com/westes/flex/releases/download/v${pkgver}/flex-${pkgver}.tar.gz"
)

sha256sums=(
    e87aae032bf07c26f85ac0ed3250998c37621d95f8bd748b31f15b33c45ee995
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=''
    make
}

package_flex() {
    pkgfiles=(
        bin
        lib/lib*.so.*
        share/man/man1/
    )
    depends=(musl)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}

package_flex-dev() {
    pkgfiles=(
        include
        lib/*.so
        lib/*.a
    )
    depends=(flex)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}
