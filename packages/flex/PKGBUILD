# Maintainer: Jeremy Huntwork <jhuntwork@lightcubesolutions.com>

pkgname=(flex flex-dev)
pkgver=2.6.3
pkgrel=1
pkgdesc='A tool for generating scanners, or tokenizers that recognize lexical \
patterns in text.'
arch=('x86_64')
url='http://flex.sourceforge.net/'
license=('GPL2')
groups=('base')
depends=()
makedepends=(bison)
options=()
changelog=ChangeLog
source=(
    "http://github.com/westes/flex/releases/download/v2.6.3/flex-${pkgver}.tar.gz"
)

sha256sums=(
    68b2742233e747c462f781462a2a1e299dc6207401dac8f0bbb316f48565c2aa
)


build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=''
    make
}

package_flex() {
    pkgfiles=(
        bin
        lib/lib*.so.*
        share/man/man1/
    )
    depends=(musl)
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdirbase}/dest" install
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}

package_flex-dev() {
    pkgfiles=(
        include
        lib/*.so
        lib/*.a
    )
    depends=(flex)
    cd "${pkgdirbase}/dest"
    set -o pipefail
    find ${pkgfiles[@]} | cpio -dump "${pkgdir}"
}
